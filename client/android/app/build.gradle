apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.vell.vchat"
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // The paths to the protoc and grpc_cpp_plugin binaries on the host system (codegen
                // is not cross-compiled to Android)
                def protoc = project.hasProperty('protoc') ?
                        project.property('protoc') : '/usr/local/bin/protoc'
                def grpc_cpp_plugin = project.hasProperty('grpc_cpp_plugin') ?
                        project.property('grpc_cpp_plugin') : '/usr/local/bin/grpc_cpp_plugin'

                cppFlags "-std=c++11 -frtti -fexceptions"
                arguments '-DANDROID_STL=c++_static'
                arguments '-DRUN_HAVE_POSIX_REGEX=0'
                arguments '-DRUN_HAVE_STD_REGEX=0'
                arguments '-DRUN_HAVE_STEADY_CLOCK=0'
                arguments '-Dprotobuf_BUILD_PROTOC_BINARIES=off'
                arguments '-DgRPC_BUILD_CODEGEN=off'
                arguments '-DPROTOBUF_PROTOC_EXECUTABLE=' + protoc
                arguments '-DGRPC_CPP_PLUGIN_EXECUTABLE=' + grpc_cpp_plugin
            }
        }
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
//            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a','armeabi'
            abiFilters 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            java {
                srcDirs = [
                        "../../generated-src/java",
                        "../../src/java",
                        "src/main/java"
                ]
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    implementation 'com.tencent.mars:mars-xlog:1.2.3'
}
